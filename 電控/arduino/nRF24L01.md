標籤: #arduino 

# 角位

![[nRF24L01腳位圖.png]]

1. `GND`: 接地
2. `VCC`: 接電，額定`3.3V`，可以承受`5V`
3. `CE`: 數位輸入，可以接在任何Digital 腳位
4. `CSN`: Chip Select Not ，可以接在任何Digital 腳位
5. `SCK`: 在 [[Arduino UNO]] 裡是 `D13` 角位
6. `MOSI`: 在 [[Arduino UNO]] 裡是 `D11` 角位
7. `MISO`: 在 [[Arduino UNO]] 裡是 `D12` 角位
8. `IRQ`: 類似除錯的腳位

> `IRQ`: normally high, but get low when:
> - transmit data
> - receive data
> - transmit failed
> 
> 上面這三個功能可以透過函式庫來決定啟用或不啟用
> `maskIRQ(bool tx_ok, bool tx_fail, bool rx_ready)`
> `tx_ok`: `true` ignores "transmit data"
> `tx_fail`: `true` ignores "transmit failed"
> `rx_ready`: `true` ignores "receive data"

# 程式: RF24.h

- [[#引入函式庫]]
- [[#製造物件]]
- [[#設定參數]]
  - [[#初始化]]
  - [[#設定頻道]]
  - [[#設定功放]]
  - [[#設定資料傳輸速度]]
  - [[#開啟讀寫通道]]
  - [[#設定為收 發模式]]
- [[#寫]]
- [[#讀]]
- [[#範例]]
  - [[#發射端]]
  - [[#接收端]]

## 引入函式庫

```arduino
#include <SPI.h>
#include "RF24.h"
```
兩個函式庫都需要引入

## 製造物件

```arduino
RF24 radio(pinCE, pinCSN);
```
製造一個名為`radio`的物件，並設定角位

- `pinCE`: CE腳位
- `pinCSN`: CSN腳位

> 注意`SCK`, `MOSI`, `MISO` 固定接在 `D13`, `D11`, `D12`，因此不需要特別指定

## 設定參數

寫在setup裡面

### 初始化

```arduino
radio.begin();
```
將`radio`物件初始化

- `radio`: `RF24`物件

### 設定頻道

```arduino
radio.setChannel(channel);
```
設定頻道

- `radio`: `RF24`物件
- `channel`: `0`-`125`，實際運作頻率會用`2400MHz`+`channel`，因此實際運作的頻率會在`2.4GHz`-`2.525GHz`

> 注意`channel`大於`83`時，會超出ISM2.4GHz免許可證頻帶(`2.4`-`2.4835`GHz)，若發射功率過高可能觸法

### 設定功放

```arduino
radio.setPALevel(RF24_PA_LOW);
```
設定`radio`物件的功放，功放大可以讓無線距離更遠，但是需要的電流也會更大

- `radio`: `RF24`物件
- `RF24_PA_LOW`: 一個常數，總共有四種常數可以選

> 四種常數:
> - `RF24_PA_MIN` = `-18dBm` / `7.0mA`
> - `RF24_PA_LOW` = `-12dBm` / `7.5mA`
> - `RF24_PA_HIGH` = `-6dBm` / `9.0mA`
> - `RF24_PA_MAX` = `0dBm` / `11.3mA`

### 設定資料傳輸速度

```arduino
radio.setDataRate(speed);
```
設定資料傳輸速度

- `radio`: `RF24`物件
- `speed`: 一個常數，總共有三種常數可以選

> 三種常數:
> - `RF24_250KBPS`: `250kbs`
> - `RF24_1MBPS`: `1Mbps`
> - `RF24_2MBPS`: `2Mbps`
> 
> - 注意nRF24晶片有兩種型號，有+號的才具有`250kbs`傳輸設定
> - 如果沒有執行`setDataRate(speed)`，預設會採用`2Mbps`

### 開啟讀寫通道

```arduino
radio.openWritingPipe(address);
```

```arduino
radio.openReadingPipe(number, address);
```
上面為開啟寫通道，下面為開啟讀通道

- `radio`: `RF24`物件
- `address`: 一個byte陣列，長度為5
- `number`: 0-5的數字(共有六個通道可以用)，代表開啟的通道，通道0預設當作接收端傳回收到訊息的回應，而通道2-5不會紀錄完整位址，只用第一位識別位址值作紀錄，因此一對一收發建議用通道1

> 通常會這樣寫:
> ```arduino
> byte address[][6] = {"1Node", "2Node"}
> radio.openWritingPipe(addresses[0]);
> //...
> radio.openReadingPipe(number, addresses[1]);
> ```
> 不同通道位址的第一個字元不能相同，如上面的`1Node`和`2Node`的`1`和`2`。不能相同的原因是第一個字元會作為識別位址值

### 設定為收/發模式

```arduino
radio.stopListening();
```

```arduino
radio.startListening();
```

上面為發射模式，下面為接收模式

- `radio`: `RF24`物件

## 寫
```arduino
radio.write(&msg, sizeof(msg));
```
傳送訊息，最多只能傳送32byte，多餘的部分會被剪掉

- `radio`: `RF24`物件
- `msg`: 字串

## 讀

```arduino
if (rf24.available(&pipe)) {
    char msg[32] = "";
    rf24.read(&msg, sizeof(msg));
    Serial.println(msg); // 顯示訊息內容 
}
```


## 範例

### 發射端
```arduino
#include <SPI.h>
#include "RF24.h"

RF24 rf24(7, 8); // CE腳, CSN腳

const byte addr[] = "1Node";
const char msg[] = "Happy Hacking!";

void setup() {
  rf24.begin();
  rf24.setChannel(83);       // 設定頻道編號
  rf24.openWritingPipe(addr); // 設定通道位址
  rf24.setPALevel(RF24_PA_MIN);   // 設定廣播功率
  rf24.setDataRate(RF24_250KBPS); // 設定傳輸速率
  rf24.stopListening();       // 停止偵聽；設定成發射模式
}

void loop() {
  rf24.write(&msg, sizeof(msg));  // 傳送資料
  delay(1000);
}
```

### 接收端

```arduino
#include <SPI.h>
#include "RF24.h"

RF24 rf24(7, 8); // CE腳, CSN腳

const byte addr[] = "1Node";
const byte pipe = 1;  // 指定通道編號

void setup() {
  Serial.begin(9600);
  rf24.begin();
  rf24.setChannel(83);  // 設定頻道編號
  rf24.setPALevel(RF24_PA_MIN);
  rf24.setDataRate(RF24_250KBPS);
  rf24.openReadingPipe(pipe, addr);  // 開啟通道和位址
  rf24.startListening();  // 開始監聽無線廣播
  Serial.println("nRF24L01 ready!");
}

void loop() {
  if (rf24.available(&pipe)) {
    char msg[32] = "";
    rf24.read(&msg, sizeof(msg));
    Serial.println(msg); // 顯示訊息內容 
  }
}
```


> 參考資料:
> [nRF24L01 教學1](https://micro.rohm.com/tw/deviceplus/inspire/interviews/nrf24l01-rf-module-tutorial-1/)
> [Use Pin IRQ to Generate Interrupt with Arduino](https://www.instructables.com/How-to-Use-the-NRF24L01s-IRQ-Pin-to-Generate-an-In/)
> [RF24 Library](https://nrf24.github.io/RF24/classRF24.html#abf68b9b0c9cd17179e9e144c3e7f9c45)
> [nRF24L01 教學2](https://swf.com.tw/?p=1044)
> [[TDK2021電路圖]]
> [[Arduino 和 樹莓派一起使用]]
> [[Arduino IoT cloud]]